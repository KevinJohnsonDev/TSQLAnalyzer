USE PlayGround
GO
DROP VIEW  IF EXISTS dbo.AutoOptionsView 
DROP TABLE IF EXISTS dbo.Automobiles;
DROP TABLE IF EXISTS dbo.MakeModelYearBodyStyleTrimOptions;
DROP TABLE IF EXISTS dbo.MakeModelYearEngines;
DROP TABLE IF EXISTS dbo.MakeModelYears;
DROP TABLE IF EXISTS dbo.Engines;
DROP TABLE IF EXISTS dbo.FuelTypes;
DROP TABLE IF EXISTS dbo.BodyStyleTrimOptions;
DROP TABLE IF EXISTS dbo.BodyStyles;
DROP TABLE IF EXISTS dbo.TrimOptions;
DROP TABLE IF EXISTS dbo.ModelYears;
DROP TABLE IF EXISTS dbo.Models;
DROP TABLE IF EXISTS dbo.Makes;
GO
CREATE TABLE dbo.Makes(
	MakeID SMALLINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_Makes_MakeID PRIMARY KEY CLUSTERED,
	Make VARCHAR(50) NOT NULL,
	CONSTRAINT CHK_DBO_MAKES_Make_NO_EXT_WHITESPACE CHECK( 
			LEN('*' + Make + '*') = LEN('*' + TRIM(Make) + '*' )
		AND Make <> ''
	),
	CONSTRAINT UQ_DBO_MAKES_Make UNIQUE(Make)
);

CREATE TABLE dbo.Models(
	ModelID SMALLINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_MODELS_ModelID PRIMARY KEY CLUSTERED,
	Model VARCHAR(50) NOT NULL,
	CONSTRAINT CHK_DBO_MODELS_Model_NO_EXT_WHITESPACE CHECK( LEN('*' + Model + '*') = LEN('*' + TRIM(Model) + '*' ) ),
	CONSTRAINT UQ_DBO_MODELS_Model UNIQUE(Model)
);
CREATE TABLE dbo.TrimOptions(
	TrimOptionID SMALLINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_TRIMOPTIONS_TrimOptionID PRIMARY KEY CLUSTERED,
	TrimOption VARCHAR(50) NOT NULL,
	CONSTRAINT CHK_DBO_TRIMOPTIONS_TrimOption_NO_EXT_WHITESPACE CHECK( LEN('*' + TrimOption + '*') = LEN('*' + TRIM(TrimOption) + '*' ) ),
	CONSTRAINT UQ_DBO_TRIMOPTIONS_TrimOption UNIQUE(TrimOption)

);
CREATE TABLE dbo.BodyStyles(
	BodyStyleID SMALLINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_BODYSTYLES_BodyStyleID PRIMARY KEY CLUSTERED,
	BodyStyle VARCHAR(50) NOT NULL CONSTRAINT UQ_BODYSTYLES_BodyStyle UNIQUE,
	CONSTRAINT CHK_DBO_BODYSTYLES_BodyStyle_NO_EXT_WHITESPACE CHECK(
			LEN('*' + BodyStyle + '*') = LEN('*' + TRIM(BodyStyle) + '*' )
		AND BodyStyle <> ''
	),
	CONSTRAINT UQ_DBO_BODYSTYLES_BodyStyle UNIQUE(BodyStyle)

);

CREATE TABLE dbo.ModelYears(
	ModelYearID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_ModelYears_ModelYearID PRIMARY KEY CLUSTERED,
	ModelID SMALLINT NOT NULL,
	ModelYear DATE NOT NULL,
	FormattedYear AS 
		CASE 
			WHEN DATEPART(Month,ModelYear) = 1 AND DATEPART(DAY,ModelYear) = 1 THEN CAST(DATEPART(YEAR,ModelYear) AS VARCHAR(10))
			ELSE CAST(ModelYear AS VARCHAR(10))
		END,
	CONSTRAINT CHK_DBO_ModelYears_ValidYear CHECK (
			ModelYear >= '1886-06-01' --Benz Patent Motor Car
		AND ModelYear <= DATEADD(YEAR,1,SYSUTCDATETIME()) --Some manufactures like to do this for some crazy reason 	
	),
	CONSTRAINT FK_DBO_ModelYears_MODELS_ModelID 
	FOREIGN KEY(ModelID)
	REFERENCES dbo.Models(ModelID)
	ON DELETE CASCADE ON UPDATE CASCADE
);

CREATE TABLE dbo.BodyStyleTrimOptions(
	BodyStyleTrimOptionID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_BODYSTYLETRIMOPTIONS_BodyStyleTrimOptionID PRIMARY KEY CLUSTERED,
	BodyStyleID SMALLINT NOT NULL,
	TrimOptionID SMALLINT NOT NULL,
	CONSTRAINT FK_DBO_BODYSTYLETRIMOPTIONS_BODYSTYLES_BodyStyleID 
		FOREIGN KEY (BodyStyleID)
		REFERENCES dbo.BodyStyles(BodyStyleID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT FK_DBO_BODYSTYLETRIMOPTIONS_TRIMOPTIONS_TrimOptionID
		FOREIGN KEY (TrimOptionID)
		REFERENCES dbo.TrimOptions(TrimOptionID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT UQ_DBO_BODYSTYLETRIMOPTIONS_BodyStyleID_TrimOptionID UNIQUE(BodyStyleID,TrimOptionID)
);


CREATE TABLE dbo.FuelTypes(
	FuelTypeID TINYINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_FUELTYPES_FuelTypeID PRIMARY KEY CLUSTERED,
	FuelType VARCHAR(50) NOT NULL,
	CONSTRAINT UQ_DBO_FUELTYPES_FuelType UNIQUE(FuelType),
	CONSTRAINT CHK_DBO_FUELTYPES_FuelType_NO_EXT_WHITESPACE CHECK(
			LEN('*' + FuelType + '*') = LEN('*' + TRIM(FuelType) + '*' )
		AND FuelType <> ''
	)
);
CREATE TABLE dbo.Engines(
	EngineID SMALLINT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_ENGINES_EngineID PRIMARY KEY CLUSTERED,
	EngineName VARCHAR(50) NOT NULL,
	CylinderCount TINYINT NOT NULL,
	FuelTypeID TINYINT NOT NULL,
	CONSTRAINT FK_DBO_ENGINES_FUELTYPES_FuelTypeID 
		FOREIGN KEY(FuelTypeID)
		REFERENCES dbo.FuelTypes(FuelTypeID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT CHK_DBO_Engines_CylinderCount_REALISTIC CHECK ( CylinderCount > 0 AND CylinderCount < 48), --48 Simon WhiteLock(UK)
	CONSTRAINT CHK_DBO_ENGINES_EngineName_NO_EXT_WHITESPACE CHECK( 
			LEN('*' + EngineName + '*') = LEN('*' + TRIM(EngineName) + '*' ) 
		AND EngineName <> ''
	)
);


CREATE TABLE dbo.MakeModelYears(
	MakeModelYearID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_MAKEMODELYEARS_MakeModelYearID PRIMARY KEY CLUSTERED,
	MakeID SMALLINT NOT NULL,
	ModelYearID INT NOT NULL,
	CONSTRAINT FK_DBO_MAKEMODELYEARS_MAKES_MakeID
		FOREIGN KEY(MakeID)
		REFERENCES dbo.Makes(MakeID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT FK_MAKEMODELYEARS_MODELYEARS_ModelYearID 
		FOREIGN kEY(ModelYearID)
		REFERENCES dbo.ModelYears(ModelYearID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT UQ_DBO_MAKEMODELYEARS_MakeID_ModelYearID UNIQUE(MakeID,ModelYearID)
);

CREATE TABLE dbo.MakeModelYearEngines(
	MakeModelYearEngineID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_MAKEMODELYEARENGINES_MakeModelYearEngineID PRIMARY KEY CLUSTERED,
	MakeModelYearID	INT NOT NULL,
	EngineID SMALLINT NOT NULL,
	CONSTRAINT FK_MAKEMODELYEARENGINES_MAKEMODELYEARS_MakeModelYearID
		FOREIGN KEY(MakeModelYearID)
		REFERENCES dbo.MakeModelYears(MakeModelYearID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT FK_MAKEMODELYEARENGINES_ENGINES_EngineID
		FOREIGN KEY(EngineID)
		REFERENCES dbo.Engines(EngineID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT UQ_DBO_MAKEMODELYEARENGINES_MakeModelYearID_EngineID UNIQUE(MakeModelYearID,EngineID)
)

CREATE TABLE dbo.MakeModelYearBodyStyleTrimOptions(
	MakeModelYearBodyStyleTrimOptionID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_MAKEMODELYEARBODYSTYLETRIMOPTIONS_MakeModelYearBodyStyleTrimOptionID PRIMARY KEY CLUSTERED,
	MakeModelYearID	INT NOT NULL,
	BodyStyleTrimOptionID INT NOT NULL,
	CONSTRAINT FK_MAKEMODELYEARBODYSTYLETRIMOPTIONS_MAKEMODELYEARS_MakeModelYearID
		FOREIGN KEY(MakeModelYearID)
		REFERENCES dbo.MakeModelYears(MakeModelYearID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT FK_MAKEMODELYEARBODYSTYLETRIMOPTIONS_BODYSTYLETRIMOPTIONS_BodyStyleTrimOptionID
		FOREIGN KEY(BodyStyleTrimOptionID)
		REFERENCES dbo.BodyStyleTrimOptions(BodyStyleTrimOptionID)
		ON DELETE CASCADE ON UPDATE CASCADE,
	CONSTRAINT UQ_DBO_MAKEMODELYEARBODYSTYLETRIMOPTIONS_MakeModelYearID_BodyStyleTrimOptionID UNIQUE(MakeModelYearID,BodyStyleTrimOptionID)
)

CREATE TABLE dbo.Automobiles(
	AutomobileID INT IDENTITY(1,1) NOT NULL CONSTRAINT PK_DBO_Automobiles_AutomobileID PRIMARY KEY CLUSTERED,
	MakeModelYearID INT NOT NULL,
	BodyStyleTrimOptionID INT NOT NULL,
	EngineID SMALLINT NOT NULL,
	CONSTRAINT FK_DBO_AUTOMOBILES_MAKEMODELYEARBODYSTYLEOPTIONS_MakeModelYearID_BodyStyleTrimOptionID
		FOREIGN KEY(MakeModelYearID,BodyStyleTrimOptionID)
		REFERENCES dbo.MakeModelYearBodyStyleTrimOptions(MakeModelYearID,BodyStyleTrimOptionID)
		ON DELETE NO ACTION ON UPDATE NO ACTION ,
	CONSTRAINT FK_DBO_AUTOMOBILES_MAKEMODELYEARENGINES_MakeModelYearID_EngineID
		FOREIGN KEY(MakeModelYearID,EngineID)
		REFERENCES dbo.MakeModelYearEngines(MakeModelYearID,EngineID)
		ON DELETE NO ACTION  ON UPDATE NO ACTION 
);
GO
CREATE VIEW dbo.AutoOptionsView 
AS
	SELECT  
			Makes.Make,
			Models.Model,
			MY.ModelYear,
			E.EngineName,
			E.CylinderCount,
			FT.FuelType,
			BS.BodyStyle,
			TOPS.TrimOption
	FROM	dbo.Automobiles AS A 			
	--JOIN    dbo.MakeModelYearBodyStyleTrimOptions AS MMYBSTO ON  A.MakeModelYearID = MMYBSTO.MakeModelYearID AND A.BodyStyleTrimOptionID = MMYBSTO.BodyStyleTrimOptionID --Commented out join is not needed as For Inegrity Purposes Only
	JOIN	dbo.BodyStyleTrimOptions AS BSTOPS ON A.BodyStyleTrimOptionID = BSTOPS.BodyStyleTrimOptionID	
		JOIN    dbo.TrimOptions AS TOPS ON BSTOPS.TrimOptionID = TOPS.TrimOptionID 
		JOIN    dbo.BodyStyles AS BS ON BS.BodyStyleID = BSTOPS.BodyStyleID
	JOIN	dbo.MakeModelYearEngines AS MMYE ON MMYE.MakeModelYearID = A.MakeModelYearID AND A.EngineID = MMYE.EngineID
		JOIN    dbo.MakeModelYears AS MMY ON MMY.MakeModelYearID = MMYE.MakeModelYearID
			JOIN    dbo.Makes AS Makes ON MMY.MakeID = Makes.MakeID
			JOIN    dbo.ModelYears AS MY ON MMY.ModelYearID = MY.ModelYearID
				JOIN    dbo.Models AS Models ON Models.ModelID = MY.ModelID
		JOIN	dbo.Engines AS E ON MMYE.EngineID = E.EngineID --Could also just be A.EngineID = E.EngineID
			JOIN    dbo.FuelTypes AS FT ON E.FuelTypeID = FT.FuelTypeID
GO